let
    // Base from balances to leverage balances and scenario context
    Base = Table.SelectColumns(BalancesTable, {"Month", "ScenarioName", "ScenarioDisplay", "CheckingBalance", "SavingsBalance"}),

    // Bring DebitCardTransactionAmount as the spend base for interchange
    JoinedDebitSpend = Table.NestedJoin(Base, {"Month", "ScenarioName"},
        Table.SelectColumns(OutflowsTable, {"Month", "ScenarioName", "DebitCardTransactionAmount"}), {"Month", "ScenarioName"},
        "DebitData", JoinKind.LeftOuter),
    ExpandedDebit = Table.ExpandTableColumn(JoinedDebitSpend, "DebitData", {"DebitCardTransactionAmount"}, {"DebitCardTransactionAmount"}),

    // Partner rates from inputs
    JoinedInterchangeRate = Table.NestedJoin(ExpandedDebit, {"Month"},
        Table.SelectRows(InterpolatedInputs, each [InputType] = "PartnerInterchangeRate"), {"Month"},
        "PartnerInterchangeRateData", JoinKind.LeftOuter),
    ExpandedInterchangeRate = Table.ExpandTableColumn(JoinedInterchangeRate, "PartnerInterchangeRateData", {"Value"}, {"PartnerInterchangeRate"}),

    JoinedInterestRate = Table.NestedJoin(ExpandedInterchangeRate, {"Month"},
        Table.SelectRows(InterpolatedInputs, each [InputType] = "PartnerInterestRate"), {"Month"},
        "PartnerInterestRateData", JoinKind.LeftOuter),
    ExpandedInterestRate = Table.ExpandTableColumn(JoinedInterestRate, "PartnerInterestRateData", {"Value"}, {"PartnerInterestRate"}),

    // Replace null rate values with 0 to avoid null math
    FilledRates = Table.ReplaceValue(ExpandedInterestRate, null, 0, Replacer.ReplaceValue, {"PartnerInterchangeRate", "PartnerInterestRate"}),

    // Compute average balances with prior month per scenario
    Grouped = Table.Group(FilledRates, {"ScenarioName", "ScenarioDisplay"}, {{"Rows", each
        let
            Sorted = Table.Sort(_, {{"Month", Order.Ascending}}),

            C_Bal = Table.Column(Sorted, "CheckingBalance"),
            C_Prev = {0} & List.RemoveLastN(C_Bal, 1),
            AvgChecking = List.Transform(List.Zip({C_Prev, C_Bal}), each ( _{0} + _{1}) / 2),

            S_Bal = Table.Column(Sorted, "SavingsBalance"),
            S_Prev = {0} & List.RemoveLastN(S_Bal, 1),
            AvgSavings = List.Transform(List.Zip({S_Prev, S_Bal}), each ( _{0} + _{1}) / 2),

            Result = Table.FromColumns(
                Table.ToColumns(Sorted) & {AvgChecking, AvgSavings},
                Table.ColumnNames(Sorted) & {"AvgCheckingBalance", "AvgSavingsBalance"}
            )
        in
            Result, type table}}),
    WithAverages = Table.Combine(Grouped[Rows]),

    // Calculations
    AddedInterchange = Table.AddColumn(WithAverages, "InterchangeRevenue", each
        [DebitCardTransactionAmount] * [PartnerInterchangeRate]
    ),
    AddedCheckingInterest = Table.AddColumn(AddedInterchange, "CheckingInterestRevenue", each
        [AvgCheckingBalance] * [PartnerInterestRate]
    ),
    AddedSavingsInterest = Table.AddColumn(AddedCheckingInterest, "SavingsInterestRevenue", each
        [AvgSavingsBalance] * [PartnerInterestRate]
    ),

    // Data types
    ChangedTypes = Table.TransformColumnTypes(AddedSavingsInterest, {
        {"Month", Int64.Type},
        {"CheckingBalance", type number},
        {"SavingsBalance", type number},
        {"DebitCardTransactionAmount", type number},
        {"PartnerInterchangeRate", type number},
        {"PartnerInterestRate", type number},
        {"AvgCheckingBalance", type number},
        {"AvgSavingsBalance", type number},
        {"InterchangeRevenue", type number},
        {"CheckingInterestRevenue", type number},
        {"SavingsInterestRevenue", type number}
    })
in
    ChangedTypes

